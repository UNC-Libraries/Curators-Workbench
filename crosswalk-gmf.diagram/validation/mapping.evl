context TextInputField {
	constraint NoEmptyVocabularyUrl {
		check : (self.vocabularyURL = null) or (not self.vocabularyURL.isEmpty())
		message : 'Text input field "'+self.label+'" has an empty string for a vocabulary URL.'
		fix {
        	title : 'Unset vocabulary URL'
        	do {
        		self.unsetVocabularyURL();
        	}
     	}
	}
	constraint EmptyValidValues {
		check : not (self.isSetValidValues() and self.getValidValues().size() == 0)
		message : 'Text input field "'+self.label+'" has an empty list of valid values.'
		fix {
        	title : 'Unset valid values'
        	do {
        		self.unsetValidValues();
        	}
     	}
	}
}

context Form {
	constraint HasOutputProfile {
		check : self.getOutputProfiles().size() <> 0
		message : 'Forms must have at least one metadata output profile.'
		fix {
			title : 'Add MODS output profile'
			do {
				self.getOutputProfiles().add(OutputProfileUtil.getMODSProfile());
			}
		}
	}
}

context MappedAttribute {
	critique HasInputOrDefault {
		check : self.defaultValue.isDefined() or self.Output.isDefined()
		message : 'Attribute is not connected to data and has no default value.'
	}
}

context InputField {
	critique HasInputInCrosswalk {
		check : not self.eContainer().eContainer().isKindOf(CrossWalk) or self.Output.isDefined()
		message : 'Input "'+self.label+'" is not connected to any data.'
	}
}